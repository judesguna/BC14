OBJECT Table 71052 Marks Header
{
  OBJECT-PROPERTIES
  {
    Date=22-11-17;
    Time=15:45:25;
    Modified=Yes;
    Version List=EDU3.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // Start 01.KATHIR
               AcademicsSetup.GET;
               IF "No. Series" = '' THEN BEGIN
                 AcademicsSetup.TESTFIELD("Marks No.");
                 NoSeriesMgt.InitSeries(AcademicsSetup."Marks No.",xRec."No. Series",0D,"No.","No. Series");
               END;
               // Stop 01.KATHIR

               // Start 02.KATHIR
               "Academic Year" := Educationvertical.GetAcademicYear;
               // Stop 02.KATHIR

               // Start 08.KATHIR
               "User ID" := USERID;
               // Stop 08.KATHIR
             END;

    CaptionML=ENU=Marks Header;
    LookupPageID=Page71055;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                // Start 03.KATHIR
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  AcademicsSetup.GET;
                                                                  NoSeriesMgt.TestManual(AcademicsSetup."Marks No.");
                                                                  "No. Series" := '';
                                                                END;
                                                                 // Stop 03.KATHIR
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Class               ;Code20        ;CaptionML=ENU=Class;
                                                   Editable=Yes }
    { 3   ;   ;Section             ;Code20        ;CaptionML=ENU=Section;
                                                   Editable=Yes }
    { 4   ;   ;Academic Year       ;Code20        ;CaptionML=ENU=Academic Year;
                                                   Editable=Yes }
    { 5   ;   ;Curriculum          ;Code10        ;CaptionML=ENU=Curriculum;
                                                   Editable=Yes }
    { 7   ;   ;Max Mark            ;Integer       ;CaptionML=ENU=Max Mark;
                                                   BlankZero=Yes;
                                                   Editable=Yes }
    { 8   ;   ;Pass Mark           ;Integer       ;OnValidate=BEGIN
                                                                // Start 05.KATHIR
                                                                IF "Pass Mark" >= "Max Mark" THEN
                                                                  ERROR(Text001);
                                                                // Stop 05.KATHIR
                                                              END;

                                                   CaptionML=ENU=Pass Mark;
                                                   BlankZero=Yes;
                                                   Editable=Yes }
    { 10  ;   ;Subject             ;Code20        ;OnValidate=BEGIN
                                                                // Start 06.KATHIR
                                                                TESTFIELD(Class);
                                                                TESTFIELD(Section);
                                                                TESTFIELD("Academic Year");
                                                                TESTFIELD(Curriculum);
                                                                TESTFIELD("Exam Type");

                                                                IF RecSubject.GET(Subject) THEN
                                                                  "Subject Description" := RecSubject.Description
                                                                ELSE
                                                                  "Subject Description" := '';
                                                                // Stop 06.KATHIR

                                                                // Start 09.KATHIR
                                                                ClassSubjects.RESET;
                                                                // Start 18.VIGNESH
                                                                //ClassSubjects.SETCURRENTKEY("Class Code",Section,Curriculum,"Academic Year",Subject);
                                                                ClassSubjects.SETCURRENTKEY(Class,Curriculum,"Academic Year",Subject);
                                                                // Stop 18.VIGNESH
                                                                ClassSubjects.SETRANGE(Class,Class);
                                                                ClassSubjects.SETRANGE(Curriculum,Curriculum);
                                                                ClassSubjects.SETRANGE("Academic Year","Academic Year");
                                                                ClassSubjects.SETRANGE(Subject,Subject);
                                                                IF ClassSubjects.FINDFIRST THEN BEGIN
                                                                  "Max Mark" := ClassSubjects."Maximum Mark";
                                                                  "Pass Mark" := ClassSubjects."Pass Mark";
                                                                END ELSE BEGIN
                                                                  "Max Mark" := 0;
                                                                  "Pass Mark" := 0;
                                                                END;
                                                                // Stop 09.KATHIR
                                                                //Start 04.KATHIR
                                                                MarksHeader.RESET;
                                                                MarksHeader.SETCURRENTKEY(Class,Section,Curriculum,"Academic Year","Exam Type",Subject);
                                                                MarksHeader.SETRANGE(Class,Class);
                                                                MarksHeader.SETRANGE(Section,Section);
                                                                MarksHeader.SETRANGE(Curriculum,Curriculum);
                                                                MarksHeader.SETRANGE("Academic Year","Academic Year");
                                                                MarksHeader.SETRANGE("Exam Type","Exam Type");
                                                                MarksHeader.SETRANGE(Subject,Subject);
                                                                IF MarksHeader.FINDFIRST THEN
                                                                  ERROR(TEXT002);
                                                                //Stop 04.KATHIR
                                                              END;

                                                   OnLookup=BEGIN
                                                              FacultySubject.RESET;
                                                              //Faculty Code,Class,Curriculum,Section Code,Subject Code,Academic Year
                                                              FacultySubject.SETCURRENTKEY("Faculty Code",Class,"Academic Year");
                                                              FacultySubject.SETRANGE(FacultySubject."Faculty Code",  Teacher);
                                                              FacultySubject.SETRANGE(FacultySubject.Class, Class);
                                                              FacultySubject.SETRANGE(FacultySubject."Academic Year" , "Academic Year");
                                                              IF PAGE.RUNMODAL(0,FacultySubject) = ACTION::LookupOK THEN BEGIN
                                                                 Subject :=  FacultySubject."Subject Code";
                                                              END;
                                                            END;

                                                   CaptionML=ENU=Subject }
    { 11  ;   ;Subject Description ;Text50        ;CaptionML=ENU=Subject Description;
                                                   Editable=No }
    { 12  ;   ;Teacher Name        ;Text50        ;CaptionML=ENU=Teacher Name }
    { 13  ;   ;Exam Type           ;Code10        ;TableRelation="Exam Code";
                                                   OnValidate=BEGIN
                                                                // Start 10.KATHIR
                                                                IF ExamCode.GET("Exam Type") THEN
                                                                  "Exam Description" := ExamCode.Description;
                                                                // Stop 10.KATHIR
                                                              END;

                                                   CaptionML=ENU=Exam Type }
    { 14  ;   ;Exam Description    ;Text50        ;CaptionML=ENU=Exam Description }
    { 15  ;   ;No. Series          ;Code20        ;CaptionML=ENU=No. Series }
    { 16  ;   ;Teacher             ;Code20        ;TableRelation="Employee Alll";
                                                   OnValidate=BEGIN
                                                                // Start 11.KATHIR
                                                                IF Employee.GET(Teacher) THEN
                                                                  "Teacher Name" := Employee."First Name";
                                                                // Stop 11.KATHIR
                                                              END;

                                                   CaptionML=ENU=Teacher }
    { 17  ;   ;Entry Completed     ;Boolean       ;CaptionML=ENU=Entry Completed }
    { 18  ;   ;Class Code          ;Code20        ;TableRelation="Class Section";
                                                   OnValidate=BEGIN
                                                                IF ClassSection.GET("Class Code") THEN BEGIN
                                                                  Class := ClassSection.Class;
                                                                  Section := ClassSection.Section;
                                                                  Curriculum := ClassSection.Curriculum;
                                                                  "Academic Year" := ClassSection."Academic Year";
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Class Code }
    { 70120;  ;User ID             ;Code20        ;CaptionML=ENU=User ID;
                                                   Editable=No }
    { 70121;  ;Portal ID           ;Code20        ;CaptionML=ENU=Portal ID }
    { 70122;  ;Created Date        ;DateTime       }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Class,Curriculum,Academic Year,Subject   }
    {    ;Class,Section,Curriculum,Academic Year,Subject }
    {    ;Class,Section,Curriculum,Academic Year,Exam Type,Subject }
    {    ;Section                                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1102155001 : TextConst 'ENU=Pass Mark Cannot be greater than Max Mark';
      RecSubject@1102155000 : Record 71004;
      Educationvertical@1102155002 : Codeunit 71880;
      AcademicsSetup@1102155003 : Record 71040;
      NoSeriesMgt@1102155004 : Codeunit 396;
      MarksHeader@1102155005 : Record 71052;
      TEXT002@1102155006 : TextConst 'ENU=Marks Header already Generated for this Class and for this subject';
      ClassSubjects@1102155007 : Record 71048;
      ExamCode@1102155008 : Record 71903;
      Employee@1102155009 : Record 5200;
      ClassSectionLook@1102155010 : Record 71015;
      ClassSection@1102155011 : Record 71015;
      FacultySubject@1102159000 : Record 71150;

    PROCEDURE Assistedit@1102155006(OldMarks@1102155000 : Record 71052) : Boolean;
    BEGIN
      // Start 07.KATHIR
      WITH OldMarks DO BEGIN
        OldMarks := Rec;
        AcademicsSetup.GET;
        AcademicsSetup.TESTFIELD("Marks No.");
        IF NoSeriesMgt.SelectSeries(AcademicsSetup."Marks No.",OldMarks."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := OldMarks;
          EXIT(TRUE);
        END;
      END;
      // Stop 07.KATHIR
    END;

    BEGIN
    {
        No   Date      Sign     Trigger                     Description
      -----------------------------------------------------------------------------------------------
        01  30/09/09   KATHIR   OnInsert()                  Code added to generate Number Series
        02  30/09/09   KATHIR   OnInsert()                  Code added to get Current Acadmemic Year
        03  30/09/09   KATHIR   No. - OnValidate()          Code added to generate Number Series
        04  30/09/09   KATHIR   Subject - OnValidate()      Code added to check Current Combination of class & Section already entered
        05  30/09/09   KATHIR   Pass Mark - OnValidate()    Code added to restrict pass mark with Max mark
        06  30/09/09   KATHIR   Subject - OnValidate()      Code added to get Subject Description
        07  30/09/09   KATHIR   AssistEdit()                Code added to generate Number Series
        08  01/10/09   KATHIR   OnInsert()                  Code added to Insert Current UserID
        09  01/10/09   KATHIR   Subject - OnValidate()      Code added to get Max mark and pass mark from class subjects
        10  01/10/09   KATHIR   Exam Type - OnValidate()    Code added to get exam type's description
        11  11/10/09   KATHIR   Teacher - OnValidate()      Code added to get teacher Name
        12  23/11/09   VIGNESH  Class - OnValidate()        Code added to get the Class Section & Curriculum
        13  23/11/09   VIGNESH  Class - OnLookup()          Code added to get the Class Section & Curriculum
        14  23/11/09   VIGNESH  Curriculum - OnValidate()   Code added to get the Class Section & Curriculum
        15  23/11/09   VIGNESH  Curriculum - OnLookup()     Code added to get the Class Section & Curriculum
        16  23/11/09   VIGNESH  Section - OnValidate()      Code added to get the Class Section & Curriculum
        17  23/11/09   VIGNESH  Section - OnLookup()        Code added to get the Class Section & Curriculum
        18  25/11/09   VIGNESH  Subject - OnValidate()      SetCurrent key Modified based on section
    }
    END.
  }
}


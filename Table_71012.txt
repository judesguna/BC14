OBJECT Table 71012 Evaluation Header
{
  OBJECT-PROPERTIES
  {
    Date=30-07-14;
    Time=11:25:27;
    Modified=Yes;
    Version List=EDU3.0;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // Start 01 VANDHANA
               AdmSetup.GET;
               IF "No Series" = '' THEN BEGIN
                 AdmSetup.TESTFIELD("Evaluation No.");
                 NoSeriesMgt.InitSeries(AdmSetup."Evaluation No.", xRec."No Series",0D,"Evaluation No.","No Series");
               END;
               // Stop 01 VANDHANA

               // Start 04 VANDHANA
               "Academic Year" := Eduvertical.GetAdmissionYear;
               // Stop 04 VANDHANA

               // Start 06. VANDHANA
               "User ID" := USERID;
               // Stop 06. VANDHANA
             END;

    OnDelete=VAR
               AppEval@1102155000 : Record 71009;
             BEGIN
               AppEval.RESET;
               AppEval.SETRANGE(AppEval."Evaluation No.","Evaluation No.");
               IF AppEval.FINDSET THEN
                 AppEval.DELETEALL;
             END;

    CaptionML=ENU=Evaluation Header;
    LookupPageID=Page71021;
    DrillDownPageID=Page71021;
  }
  FIELDS
  {
    { 1   ;   ;Evaluation No.      ;Code10        ;OnValidate=BEGIN
                                                                // Start 02 VANDHANA
                                                                IF "Evaluation No." <> xRec."Evaluation No." THEN BEGIN
                                                                  AdmSetup.GET;
                                                                  NoSeriesMgt.TestManual(AdmSetup."Evaluation No.");
                                                                  "No Series" := '';
                                                                END;
                                                                // Stop 02 VANDHANA
                                                              END;

                                                   CaptionML=ENU=Evaluation No. }
    { 2   ;   ;Class               ;Code10        ;TableRelation="Class Card";
                                                   OnValidate=BEGIN
                                                                // Start 07.VIGNESH
                                                                ClassCardLook.ValidateClass(Class,Curriculum,"Academic Year");
                                                                // Stop 07.VIGNESH
                                                              END;

                                                   OnLookup=BEGIN
                                                              // Start 08.VIGNESH
                                                              ClassCardLook.LookUpClass(Class,Curriculum,"Academic Year");
                                                              // Stop 08.VIGNESH
                                                            END;

                                                   CaptionML=ENU=Class }
    { 3   ;   ;Curriculum          ;Code10        ;TableRelation="Class Card".Curriculum;
                                                   OnValidate=BEGIN
                                                                // Start 09.VIGNESH
                                                                ClassCardLook.ValidateCurriculum(Class,Curriculum,"Academic Year");
                                                                // Stop 09.VIGNESH
                                                              END;

                                                   OnLookup=BEGIN
                                                              // Start 10.VIGNESH
                                                              ClassCardLook.LookUpCurriculum(Class,Curriculum,"Academic Year");
                                                              // Stop 10.VIGNESH
                                                            END;

                                                   CaptionML=ENU=Curriculum }
    { 4   ;   ;Academic Year       ;Code10        ;TableRelation="Academic Year";
                                                   CaptionML=ENU=Academic Year }
    { 5   ;   ;Evaluation Code     ;Code10        ;TableRelation="Class Evaluation"."Evaluation Method Code" WHERE (Class=FIELD(Class),
                                                                                                                    Curriculum=FIELD(Curriculum),
                                                                                                                    Academic Year=FIELD(Academic Year));
                                                   OnValidate=BEGIN
                                                                // Start 05.KATHIR
                                                                EvaluationHeader.RESET;
                                                                EvaluationHeader.SETCURRENTKEY(Class,Curriculum,"Academic Year","Evaluation Code");
                                                                EvaluationHeader.SETRANGE(Class,Class);
                                                                EvaluationHeader.SETRANGE(Curriculum,Curriculum);
                                                                EvaluationHeader.SETRANGE("Academic Year","Academic Year");
                                                                EvaluationHeader.SETRANGE("Evaluation Code","Evaluation Code");
                                                                IF EvaluationHeader.FINDFIRST THEN
                                                                  ERROR(Text000);
                                                                // Stop 05.KATHIR
                                                              END;

                                                   CaptionML=ENU=Evaluation Code }
    { 6   ;   ;No Series           ;Code20        ;CaptionML=ENU=No Series }
    { 70120;  ;User ID             ;Code20        ;CaptionML=ENU=User ID }
    { 70121;  ;Portal ID           ;Code20        ;CaptionML=ENU=Portal ID }
  }
  KEYS
  {
    {    ;Evaluation No.                          ;Clustered=Yes }
    {    ;Class,Curriculum,Academic Year,Evaluation Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AdmSetup@1102155000 : Record 71000;
      NoSeriesMgt@1102155001 : Codeunit 396;
      EvaluationHeader1@1102155002 : Record 71012;
      Eduvertical@1102155003 : Codeunit 71880;
      EvaluationHeader@1102155004 : Record 71012;
      ClassCardLook@1102155005 : Record 71006;
      Text000@1102155006 : TextConst 'ENU=Evaluation is already  done for this class.';

    PROCEDURE Assistedit@1102155000(OldEvaluation@1102155000 : Record 71012) : Boolean;
    BEGIN
      // Start 03 VANDHANA
      WITH EvaluationHeader1 DO BEGIN
        OldEvaluation := Rec;
        AdmSetup.GET;
        AdmSetup.TESTFIELD("Evaluation No.");
        IF NoSeriesMgt.SelectSeries(AdmSetup."Evaluation No.",OldEvaluation."No Series","No Series") THEN BEGIN
          NoSeriesMgt.SetSeries("Evaluation No.");
          Rec := EvaluationHeader1;
          EXIT(TRUE);
        END;
      END;
      // Stop 03 VANDHANA
    END;

    BEGIN
    {
         No     Date    Sign     Trigger                 Description
      -----------------------------------------------------------------------------------------------
        01     07/10/09 VANDHANA OnInsert()                      Code added for No.series
        02     07/10/09 VANDHANA OnValidate()                    Code added for No.series
        03     07/10/09 VANDHANA Assistedit()                    Code added for No.series
        04     08/10/09 VANDHANA OnInsert()                      Code to get Admission year
        05     14/10/09 KATHIR   Evaluation Code - OnValidate()  Code added to check duplicate Entries
        06     19/10/09 VANDHANA OnInsert                        Code to assign User ID.
        07     23/11/09 VIGNESH  Class - OnValidate()            Code added to get the Class & Curriculum value
        08     23/11/09 VIGNESH  Class - OnLookup()              Code added to get the Class & Curriculum value
        09     23/11/09 VIGNESH  Curriculum - OnValidate()       Code added to get the Class & Curriculum value
        10     23/11/09 VIGNESH  Curriculum - OnLookup()         Code added to get the Class & Curriculum value
    }
    END.
  }
}


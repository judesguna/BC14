OBJECT Table 72120 Employee Loans
{
  OBJECT-PROPERTIES
  {
    Date=30-07-14;
    Time=12:42:38;
    Modified=Yes;
    Version List=FRHRPR4.00.03.06;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Location Code" = '' THEN BEGIN
                 HRPayrollSetup.GET(USERID);
                 LocationHRPayrollSetup.GET(HRPayrollSetup."Location Code");
                 LocationHRPayrollSetup.TESTFIELD("Loan No.");
                 NoSeriesMgt.InitSeries(LocationHRPayrollSetup."Loan No.",
                 xRec."No. Series",0D,"Loan Id","No. Series");
               END;

               "Loan Sanctioned Date" := TODAY;
               "Created Date" := TODAY;
               "Created User Id" := USERID;
             END;

    OnModify=BEGIN
               "Modified Date" := TODAY;
               "Modified User Id" := USERID;
             END;

    OnDelete=BEGIN
               //ERROR('You Cannot Delete Posted Transaction');
             END;

    CaptionML=ENU=Employee Loans;
    LookupPageID=Page72166;
    DrillDownPageID=Page72166;
  }
  FIELDS
  {
    { 1   ;   ;Loan Id             ;Code20        ;OnValidate=BEGIN
                                                                IF "Loan Id" <> xRec."Loan Id" THEN BEGIN
                                                                  HRPayrollSetup.GET(USERID);
                                                                  LocationHRPayrollSetup.GET(HRPayrollSetup."Location Code");
                                                                  NoSeriesMgt.TestManual(LocationHRPayrollSetup."Loan No.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Loan Id }
    { 2   ;   ;Employee No         ;Code20        ;TableRelation=Employee;
                                                   OnValidate=BEGIN
                                                                IF Employee.GET("Employee No") THEN BEGIN
                                                                  "Location Code" := Employee."Location Code";
                                                                  "Salary Plan Code" := Employee."Salary Plan Code";
                                                                  Name := Employee."First Name" + '.' + Employee.Initials;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Employee No }
    { 3   ;   ;Loan Code           ;Code20        ;TableRelation="Pay Elements" WHERE (Location Code=FIELD(Location Code),
                                                                                       Salary Plan Code=FIELD(Salary Plan Code),
                                                                                       Loan Element=CONST(Yes));
                                                   OnValidate=BEGIN
                                                                IF PayElements.GET("Loan Code","Location Code","Salary Plan Code") THEN BEGIN
                                                                  "Purpose of Loan" := PayElements.Description;
                                                                  "Loan Sanctioned Amount" := PayElements.Amount;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Loan Code }
    { 4   ;   ;Location Code       ;Code20        ;TableRelation="Location HR & Payroll Setup";
                                                   CaptionML=ENU=Location Code;
                                                   Editable=No }
    { 5   ;   ;Salary Plan Code    ;Code20        ;TableRelation="Salary Plan";
                                                   CaptionML=ENU=Salary Plan Code;
                                                   Editable=No }
    { 6   ;   ;Name                ;Text50        ;CaptionML=ENU=Name;
                                                   Editable=No }
    { 7   ;   ;Purpose of Loan     ;Text50        ;CaptionML=ENU=Purpose of Loan }
    { 8   ;   ;Loan Starting Date  ;Date          ;CaptionML=ENU=Loan Starting Date }
    { 9   ;   ;Loan Sanctioned Date;Date          ;CaptionML=ENU=Loan Sanctioned Date }
    { 10  ;   ;Loan Sanctioned Amount;Decimal     ;OnValidate=BEGIN
                                                                TESTFIELD("Loan Starting Date");
                                                                ValidateBalanceAmount;
                                                              END;

                                                   CaptionML=ENU=Loan Sanctioned Amount }
    { 11  ;   ;Deduction Amount    ;Decimal       ;OnValidate=BEGIN
                                                                ValidateBalanceAmount;
                                                              END;

                                                   CaptionML=ENU=Deduction Amount }
    { 12  ;   ;Total Loans Deducted;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Processed Salary"."Payable Amount" WHERE (Location Code=FIELD(Location Code),
                                                                                                              Employee No=FIELD(Employee No),
                                                                                                              Branch Code=FIELD(Loan Id)));
                                                   CaptionML=ENU=Total Loans Deducted;
                                                   Editable=No }
    { 13  ;   ;Loan Balance Amount ;Decimal       ;CaptionML=ENU=Loan Balance Amount;
                                                   Editable=No }
    { 14  ;   ;Repayment Date      ;Date          ;CaptionML=ENU=Repayment Date }
    { 15  ;   ;Repayment Amount    ;Decimal       ;OnValidate=BEGIN
                                                                ValidateBalanceAmount;
                                                              END;

                                                   CaptionML=ENU=Repayment Amount }
    { 16  ;   ;No Deduction Request;Boolean       ;CaptionML=ENU=No Deduction Request }
    { 17  ;   ;Completed           ;Boolean       ;CaptionML=ENU=Completed }
    { 18  ;   ;Created Date        ;Date          ;CaptionML=ENU=Created Date }
    { 19  ;   ;Created User Id     ;Code20        ;CaptionML=ENU=Created User Id }
    { 20  ;   ;Modified Date       ;Date          ;CaptionML=ENU=Modified Date }
    { 21  ;   ;Modified User Id    ;Code20        ;CaptionML=ENU=Modified User Id }
    { 40  ;   ;No. Series          ;Code20        ;TableRelation="No. Series";
                                                   CaptionML=ENU=No. Series }
  }
  KEYS
  {
    {    ;Loan Id                                 ;Clustered=Yes }
    {    ;Employee No                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      HRPayrollSetup@1000000000 : Record 72051;
      LocationHRPayrollSetup@1000000005 : Record 72052;
      NoSeriesMgt@1000000004 : Codeunit 396;
      Employee@1000000003 : Record 5200;
      PayElements@1102159001 : Record 72060;
      EndDate@1000000002 : Date;

    PROCEDURE AssistEdit@2(OldEmployeeLoans@1000 : Record 72120) : Boolean;
    VAR
      EmployeeLoans@1102159000 : Record 72120;
    BEGIN
      WITH EmployeeLoans DO BEGIN
        EmployeeLoans := Rec;
        HRPayrollSetup.GET(USERID);
        LocationHRPayrollSetup.GET(HRPayrollSetup."Location Code");
        LocationHRPayrollSetup.TESTFIELD("Loan No.");
        IF NoSeriesMgt.SelectSeries(LocationHRPayrollSetup."Loan No.",
           OldEmployeeLoans."No. Series","No. Series")
        THEN BEGIN
          NoSeriesMgt.SetSeries("Loan Id");
          Rec := EmployeeLoans;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE ValidateBalanceAmount@1102159000();
    BEGIN
      CALCFIELDS("Total Loans Deducted");
      "Loan Balance Amount" := "Loan Sanctioned Amount" - "Total Loans Deducted" - "Repayment Amount";
    END;

    BEGIN
    {
      -----------------------------------------------------------------------------------------------
      Firstware Sofware Solutions : Project Name : HR & PAYROLL
      -----------------------------------------------------------------------------------------------
      No.  Date          Developer     Spec/CU/CR      Description
      -----------------------------------------------------------------------------------------------
      1    04.APR.2009   RAJAH.A                       New Tables Added for Payroll Module.
      -----------------------------------------------------------------------------------------------
    }
    END.
  }
}


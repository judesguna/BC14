OBJECT Codeunit 71884 Education Calendar
{
  OBJECT-PROPERTIES
  {
    Date=31-07-14;
    Time=02:33:54;
    Modified=Yes;
    Version List=EDU3.0;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE "Generate Calendar"@1102155000(EduCode@1102155006 : Code[20];AcaYear@1102155008 : Code[20]);
    VAR
      Date1@1102155005 : Record 2000000007;
      EduCalendarEntry@1102155004 : Record 71922;
      EduCalendarOffDaySetup@1102155003 : Record 71923;
      IntDay@1102155002 : Integer;
      PeriodHeader@1102155001 : Record 71510;
      EduCalendarHolidaySetup@1102155000 : Record 71924;
      EduCalendar@1102155007 : Record 71921;
    BEGIN
      EduCalendar.GET(EduCode);
      EduCalendarEntry.RESET;
      EduCalendarEntry.SETRANGE(EduCalendarEntry.Code,EduCode);
      IF EduCalendarEntry.FINDFIRST THEN BEGIN
        EduCalendarEntry.MODIFYALL("Off Day",FALSE);
        EduCalendarEntry.MODIFYALL(Holiday,FALSE);
        EduCalendarEntry.MODIFYALL("Description-1",'');
      END;

      Date1.RESET;
      Date1.SETFILTER("Period Type",'%1',Date1."Period Type"::Date);
      Date1.SETFILTER("Period Start",'%1..%2',EduCalendar."Start Date",EduCalendar."End Date");
      IF Date1.FINDFIRST THEN
        REPEAT
          EduCalendarEntry.SETFILTER(Code,'%1',EduCalendar.Code);
          EduCalendarEntry.SETFILTER(Date,'%1',Date1."Period Start");
          IF NOT EduCalendarEntry.FINDFIRST THEN BEGIN
            EduCalendarEntry.Code := EduCalendar.Code;
            EduCalendarEntry.Date := Date1."Period Start";
            EduCalendarEntry.Day := Date1."Period No.";
            EduCalendarEntry."Day Order" := 0;
            EduCalendarEntry.INSERT;
          END;
        UNTIL Date1.NEXT = 0;

      EduCalendarOffDaySetup.RESET;
      EduCalendarOffDaySetup.SETFILTER(Code,'%1',EduCalendar.Code);
      IF EduCalendarOffDaySetup.FINDFIRST THEN
        REPEAT
          EduCalendarEntry.RESET;
          EduCalendarEntry.SETFILTER(Code,'%1',EduCalendarOffDaySetup.Code);
          EduCalendarEntry.SETFILTER(Day,'%1',EduCalendarOffDaySetup.WeekDay);
          IF EduCalendarEntry.FINDFIRST THEN
            REPEAT
              EduCalendarEntry."Off Day" := TRUE;
              EduCalendarEntry.MODIFY;
            UNTIL EduCalendarEntry.NEXT = 0;
        UNTIL EduCalendarOffDaySetup.NEXT = 0;

      EduCalendarHolidaySetup.RESET;
      EduCalendarHolidaySetup.SETRANGE(Code,EduCalendar.Code);
      IF EduCalendarHolidaySetup.FINDFIRST THEN
        REPEAT
          EduCalendarEntry.RESET;
          EduCalendarEntry.SETRANGE(Code,EduCalendarHolidaySetup.Code);
          EduCalendarEntry.SETRANGE(Date,EduCalendarHolidaySetup."Holiday Date");
          IF EduCalendarEntry.FINDFIRST THEN
            REPEAT
              EduCalendarEntry.Holiday := TRUE;
              EduCalendarEntry."Description-1" := EduCalendarHolidaySetup.Description;
              EduCalendarEntry.MODIFY;
            UNTIL EduCalendarEntry.NEXT = 0;
        UNTIL EduCalendarHolidaySetup.NEXT = 0
    END;

    PROCEDURE AssignDayorder@1102155001();
    VAR
      PeriodHeader@1102155001 : Record 71510;
      EduCalendarEntry@1102155002 : Record 71922;
      IntDay@1102155003 : Integer;
    BEGIN
      PeriodHeader.FINDFIRST;
      IntDay := 1;
      EduCalendarEntry.RESET;
      EduCalendarEntry.SETRANGE("Off Day",FALSE);
      EduCalendarEntry.SETRANGE(Holiday,FALSE);
      IF EduCalendarEntry.FINDFIRST THEN
        REPEAT
          IF IntDay = PeriodHeader."Working Days Per Week" + 1 THEN
            IntDay := 1;
          EduCalendarEntry."Day Order" := IntDay;
          EduCalendarEntry.MODIFY;
          IntDay += 1;
        UNTIL EduCalendarEntry.NEXT = 0;
    END;

    BEGIN
    {
         No     Date         Sign          Trigger                     Description
      ------------------------------------------------------------------------------------------------------------------------------------
        01     29.12.09     ABK        GenerateCalender()          Function to generate the education calender based on edu setup.
    }
    END.
  }
}

